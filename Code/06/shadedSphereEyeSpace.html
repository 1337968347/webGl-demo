<!DOCTYPE html>
<html>

<script id="vertex-shader" type="x-shader/x-vertex">

// assume both position and normal are in homogeneous form

attribute vec4 vPosition;
attribute vec4 vNormal;

varying vec4 fColor;


uniform vec4 ambientProduct, diffuseProduct, specularProduct;
uniform mat4 modelViewMatrix;
uniform mat4 projectionMatrix;
uniform vec4 lightPosition;
uniform float shininess;
uniform mat3 normalMatrix;


void
main()
{
    
    // pos is vertex position in eye coordinates
    
    vec3 pos = (modelViewMatrix * vPosition).xyz;
    
    // vector from vertex position to light source
    
    vec3 L;
    
    // check for directional light
    // x y z w 
    // w === 0 代表这个光源是一个远距离光源 
    // 如果是远距离光源，那么光线的位置就是光线的方向
    if(lightPosition.w == 0.0) L = normalize(lightPosition.xyz);
    else L = normalize( lightPosition.xyz - pos );

    // Because the eye point the is at the orgin
    // the vector from the vertex position to the eye is
    // 观察向量
    vec3 E = -normalize( pos );
    
    // halfway vector
    // 半角向量
    vec3 H = normalize( L + E );

    // Transform vertex normal into eye coordinates
    
    // vec3 N = normalize( normalMatrix*vNormal.xyz);
    
    // as long as there is no nonuniform scaling 
    // we don't need the normal matrix and can use
    // 平面法向量
    vec3 N = normalize((modelViewMatrix*vNormal).xyz);

    // Compute terms in the illumination equation
    // 环境光反射
    vec4 ambient = ambientProduct;
    // 漫反射 光线跟平面的夹角
    float Kd = max( dot(L, N), 0.0 );
    vec4  diffuse = Kd*diffuseProduct;
    // 镜面反射 =  半角向量 跟 法向量 的余弦值的 n次方 *  光线效果 * 距离
    float Ks = pow( max(dot(N, H), 0.0), shininess );
    vec4  specular = Ks * specularProduct;
    
    if( dot(L, N) < 0.0 ) {
	specular = vec4(0.0, 0.0, 0.0, 1.0);
    } 

    gl_Position = projectionMatrix * modelViewMatrix * vPosition;
    
    fColor = ambient + diffuse +specular;

    fColor.a = 0.9;
}
</script>

<script id="fragment-shader" type="x-shader/x-fragment">

precision mediump float;

varying vec4 fColor;

void
main()
{

    gl_FragColor = fColor;

}
</script>

<p> </p>
<button id = "Button0">Increase R</button>
<button id = "Button1">Decrease R</button>

<p> </p>
<button id = "Button2">Increase theta</button>
<button id = "Button3">Decrease theta</button>
<button id = "Button4">Increase phi</button>
<button id = "Button5">Decrease phi</button>
<p> </p>
<button id = "Button6">Increase Subdivisions</button>
<button id = "Button7">Decrease Subdivisions</button>

<p></p>

<script type="text/javascript" src="../Common/webgl-utils.js"></script>
<script type="text/javascript" src="../Common/initShaders.js"></script>

<script type="text/javascript" src="../Common/MV.js"></script>
<script type="text/javascript" src="shadedSphereEyeSpace.js"></script>

<body>
<canvas id="gl-canvas" width="512" height="512">
Oops ... your browser doesn't support the HTML5 canvas element
</canvas>
</body>
</html>
